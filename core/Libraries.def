;Linkers to system libraries

;---<Некоторые служебные функции>-----------------------------------------------
7-seg:
    call    sthex1
    lxi     b,STR-7seg
    add     c
    mov     c,a
    ldax    b
    ret
STR-7seg:
    .db     3FH
    .db     06H
    .db     5BH
    .db     4FH
    .db     66H
    .db     6DH
    .db     7DH
    .db     07H
    .db     7FH
    .db     6FH
    .db     77H
    .db     7CH
    .db     58H
    .db     5EH
    .db     79H
    .db     71H

sthex1:
    sui     30H
    cpi     0AH
    rm
    sui     07H
    ret
shex1:
    adi     30H
    cpi     3AH
    rm
    adi     07H
    ret

.include /home/victor/Desktop/Micron_BIOS/Standart_arithmetic.asm
.include /home/victor/Desktop/Micron_BIOS/AFS3.asm

; (E) Barsotion KY
; Функция Delay_ms_6_ - программная задержка
; t = (6001*HL)/fтакт мс
; t = HL миллисекунд при fтакт = 6.0 МГц
; Ввод:    HL (время в мс)
; Вывод:   нет
; Используемые регистры: АF,DE,HL
; Используемая память: нет
; Длина: 16 байт
; Время выполнения: ~HL мкс при тактовой частоте 6MHz
Delay_ms_6:
    lxi     d,$00F9
delay_ms_6_1:
    dcx     d
    mov     a,d
    ora     e
    jnz     delay_ms_6_1
    dcx     h
    mov     a,h
    ora     l
    jnz     Delay_ms_6
    ret



;---<Системная функция, выполняется в режиме ОС>--------------------------------

.include /home/victor/Desktop/Laulaja_tests/GrLaulaja.asm


Function:
    call    SYS_OS_muutos
    lda     $8010
    ani     $0F
    call    shex1
    call    7-seg
    cma
    out     DISP_PORT
    ret

Laulaja_4_COMM_OUT:
    out     $20
    out     $60
    out     $20
    ret

Laulaja_4_DATA_OUT:
    out     $A0
    out     $E0
    out     $A0
    ret



.def DISP_COMM_OUT =    Laulaja_4_COMM_OUT
.def DISP_DATA_OUT =    Laulaja_4_DATA_OUT



; Функция ST7920_INI - инициализация дисплея на ST7920 в граф. режиме
; Ввод: нет
; Вывод: нет
; Используемые регистры: АF,C
; Используемая память: нет
; Используемые функции:
;  - DISP_COMM_OUT
;  - DISP_DATA_OUT
;  - MacroDelay
; Длина: 29 байт
; Время выполнения: 11696 тактов

ST7920_INI_USR:
    call    SYS_OS_muutos
ST7920_INI:
    lxi     h,$F800
    shld    DISPLAY_BUFFER_ADDR
    mvi     a,30H
    call    DISP_COMM_OUT
    mvi     a,0CH
    call    DISP_COMM_OUT
    mvi     a,30H
    call    DISP_COMM_OUT
    mvi     a,01H
    call    DISP_COMM_OUT
    mvi     a,FFH
    call    MacroDelay
    mvi     a,FFH
    call    MacroDelay
    mvi     a,34H
    call    DISP_COMM_OUT
    mvi     a,02H
    call    DISP_COMM_OUT
    mvi     a,36H
    call    DISP_COMM_OUT
    ret

; (E) Barsotion KY
; Функция MacroDelay (добавлена для совместимости с ПО BarsikOS1.1)
; Используется для организации программных задержек.
; Время задержки рассчитывается по формуле: t = (20*A+25)/fтакт мкс
; Ввод: А
; Вывод: нет
; Используемые регистры: АF
; Используемая память: нет
; Длина: 5 байт
; Время выполнения: 20*A+25 тактов
MacroDelay:
    ; в А - операнд
    sui 01H
    jnz MacroDelay
    ret

; Функция ST7920_MAS - перенос массива 1кБ в видеопамять дисплея
; Ввод: stack+2 - начальный ардес массива в памяти
; Вывод: нет
; Используемые регистры: АF,C,DE,HL
; Читаемая память:
; Используемая память:
;  - disp_addr_x (byte)
;  - disp_addr_y (byte)
; Используемые функции:
;  - LCD_COMM_OUT
;  - LCD_DATA_OUT
;  - MacroDelay
; Длина: -
; Время выполнения: -

ST7920_MAS:
;работа со стеком
    pop     h
    xthl
    mvi     a,$02           ;счетчик циклов
    push    psw
;установка начальных адресов X и Y
    mvi     d,$80           ;D <- X
    mov     e,d             ;E <- Y
;Здесь: сначала печатаются $20 строк с Xнач.=$80, Yнач.=$80, Y инкрементируется
;с каждой новой строки. Потом: $20 строк с Xнач.=$88, Yнач.=$80
gr_mas_2:
    mov     a,e             ;отправка адреса Y
    call    DISP_COMM_OUT
    mov     a,d             ;отправка адреса X
    call    DISP_COMM_OUT
    mvi     b,10H           ;счетчик в B
gr_mas_1:                   ;печатаем на экран 16 байт из памяти
    mov     a,m             ; 
    call    DISP_DATA_OUT    ;
    inx     h
    dcr     b
    jnz     gr_mas_1        ;Зацикливание 1
    inr     e               ;Инкремент адреса Y
    mov     a,e             ;Смотрим, не равен ли Y 80H + 20H
    cpi     A0H             ;
    jnz     gr_mas_2        ;Зацикливание 2
    pop     psw
    dcr     a
    rz
    push    psw
    mvi     e,$80           ;Y <- $80
    mvi     d,$88           ;X <- $88
    jmp     gr_mas_2


; Функция ST7920_PRINT_BUF - перенос содержимого буфера в видеопамять дисплея
; Ввод: (DISPLAY_BUFFER_ADDR) (word) - адрес начала буфера в памяти
; Вывод: нет
; Используемые регистры: АF,C,DE,HL
; Читаемая память:
;  - start_buf_addr (word)
; Используемая память: нет
; Используемые функции:
;  - lcdCommOut
;  - lcdDataOut
;  - MacroDelay
;  - GR_MAS
; Длина: 
; Время выполнения:

ST7920_PRINT_BUF_USR:
    call    SYS_OS_muutos
ST7920_PRINT_BUF:
    lhld    DISPLAY_BUFFER_ADDR
    push    h
    call    ST7920_MAS
    ret


; Функция ST7920_BUF_CLR - очистка буфера
; Ввод:  (DISPLAY_BUFFER_ADDR) - Адрес начала буфера в памяти
; Вывод: нет
; Используемые регистры: AF,С,DE,HL
; Читаемая память:
;  - start_buf_addr
; Используемая память: нет
; Используемые функции:
;  - MFILL
; Длина: 12 байт
; Время выполнения: 37960 тактов

ST7920_BUF_CLR_USR:
    call    SYS_OS_muutos
ST7920_BUF_CLR:
    lxi     d,0400H
    lhld    DISPLAY_BUFFER_ADDR
    xra     a
    call    MFILL
    ret
